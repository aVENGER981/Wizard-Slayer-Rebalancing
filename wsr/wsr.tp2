/*************************************************************************
 *                    Wizard Slayer Rebalancing
 *                         -=by aVENGER=-
 *                        www.shsforums.net
 *************************************************************************/


BACKUP ~wsr/backup~
AUTHOR "aVENGER"                                                                   // Author and e-mail address
VERSION ~v1.12~                                                                    // Mod version
ALWAYS                                                                             // Always do this block
INCLUDE ~wsr/lib/regexp.tph~                                                       // allows regexp to match tabs and newlines (from the G3 BG2 Fixpack)
INCLUDE ~wsr/lib/tobexfix.tph~                                                     // if TobEx is installed, this disables some of its currently buggy options
LAM bgee_language                                                                  // use UTF-8 encoding on BG:EE
END                                                                                // End the ALWAYS block

README ~wsr/readme_wsr.html~                                                       // Offer to display the main readme
ASK_EVERY_COMPONENT                                                                // Disallow installing all components at once


LANGUAGE ~English~ ~ENGLISH~ ~wsr/tra/english/wsr.tra~
LANGUAGE ~Russian~ ~RUSSIAN~ ~wsr/tra/english/wsr.tra~ ~wsr/tra/russian/wsr.tra~
LANGUAGE ~French~  ~FRENCH~  ~wsr/tra/english/wsr.tra~ ~wsr/tra/french/wsr.tra~
LANGUAGE ~ºÚÃÂ÷–Œƒ (Simplified Chinese)~  ~CHS~  ~wsr/tra/english/wsr.tra~ ~wsr/tra/chs/wsr.tra~
LANGUAGE %¡c≈È§§§Â (Traditional Chinese)% ~CHT~  ~wsr/tra/english/wsr.tra~ ~wsr/tra/cht/wsr.tra~
LANGUAGE ~German~  ~GERMAN~  ~wsr/tra/english/wsr.tra~ ~wsr/tra/german/wsr.tra~
LANGUAGE ~Espa§ol por Lisandro acompa§ado por William Imm~ ~SPANISH~ ~wsr/tra/spanish/wsr.tra~
LANGUAGE ~Polish~ ~POLISH~ ~wsr/tra/polish/wsr.tra~
LANGUAGE ~Italian~ ~ITALIAN~ ~wsr/tra/italian/wsr.tra~





///////////////////////////////////////////////////////////////////////
// Wizard Slayer kit revision
///////////////////////////////////////////////////////////////////////


BEGIN @100 DESIGNATED 100 // Wizard Slayer kit revision
REQUIRE_PREDICATE ENGINE_IS ~tob bgee bg2ee iwdee~ @90                                   // ToB engine required


// Spell files


COPY    ~wsr/spl/rr#wsm2.spl~                ~override~                            // +2% Magic Resistance bonus

COPY    ~wsr/spl/rr#wssf1.spl~               ~override~                            // Arcane spell failure innate, Part 1 + initial 10% magic resistance
COPY    ~wsr/spl/rr#wssf2.spl~               ~override~                            // Arcane spell failure innate, Part 2
COPY    ~wsr/eff/rr#wssf1.eff~               ~override~                            // New arcane spell failure innate effect (shell spell application)
COPY    ~wsr/eff/rr#wssf2.eff~               ~override~                            // Spell failure display icon effect

COPY    ~wsr/spl/rr#wssm1.spl~               ~override~                            // Shatter Magic ability, Part 1
  SAY NAME1 @102 SAY NAME2 @102 SAY UNIDENTIFIED_DESC @104 SAY DESC @104
  READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
  READ_LONG  0x6a "fx_off"
  FOR (index = 0 ; index < abil_num ; index = index + 1 ) BEGIN // cycle through abilities
    READ_SHORT  ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
    READ_SHORT  ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
    FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
     READ_SHORT ("%fx_off%" +        (0x30 * ("%index2%" + "%abil_fx_idx%"))) "opcode"
     READ_LONG  ("%fx_off%" + 0x04 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "param1"
      PATCH_IF (("%opcode%" = 206) AND ("%param1%" = 74011)) BEGIN                 // display string: "You cannot cast this spell..."
       SAY ("%fx_off%" + 0x04 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) @103      // You cannot use this ability until your innate magic resistance recovers.
      END
      PATCH_IF ((GAME_IS ~iwdee~) AND ("%opcode%" = 139) AND ("%param1%" = 14790)) BEGIN
       SAY ("%fx_off%" + 0x04 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) #16967  // display string: Magic Resistance Lowered
      END
    END
  END
COPY    ~wsr/spl/rr#wssm2.spl~               ~override~                            // Shatter Magic ability, Part 2
  READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
  READ_LONG  0x6a "fx_off"
  FOR (index = 0 ; index < abil_num ; index = index + 1 ) BEGIN // cycle through abilities
    READ_SHORT  ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
    READ_SHORT  ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
    FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
     READ_SHORT ("%fx_off%" +        (0x30 * ("%index2%" + "%abil_fx_idx%"))) "opcode"
     READ_LONG  ("%fx_off%" + 0x04 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "param1"
      PATCH_IF (("%opcode%" = 206) AND ("%param1%" = 74011)) BEGIN                 // display string: "You cannot cast this spell..."
       SAY ("%fx_off%" + 0x04 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) @103      // You cannot use this ability until your innate magic resistance recovers.
      END
    END
  END
COPY    ~wsr/eff/rr#wssm1.eff~               ~override~                            // Shatter Magic effect (shell spell application)
COPY    ~wsr/bam/rr#wssmb.bam~               ~override~                            // Shatter Magic BAM icon

// 2DA modifications

ACTION_IF !FILE_EXISTS_IN_GAME ~OH3000.ARE~ THEN BEGIN                             // not on BG:EE
  STRING_SET ~25203~ @101                                                          // New Wizard Slayer kit description
END

ACTION_IF FILE_EXISTS_IN_GAME ~OH3000.ARE~ THEN BEGIN                              // BG:EE check
  STRING_SET ~25361~ @101                                                          // New Wizard Slayer kit description
  STRING_SET ~24285~ @101                                                          // New Wizard Slayer kit description (character sheet)
END

ACTION_IF FILE_EXISTS_IN_GAME ~HOWPARTY.2DA~ THEN BEGIN                            // IWD:EE check
  STRING_SET ~25203~ @101                                                          // New Wizard Slayer kit description
  STRING_SET ~37097~ @101                                                          // New Wizard Slayer kit description (character sheet)
END

COPY_EXISTING ~dualclas.2da~ ~override~                                            // dual-class list
  SET_2DA_ENTRY_LATER ~RR#WSDC~ 28 2 ~0~                                           // prevent Wizard Slayers from dual-classing into clerics
  SET_2DA_ENTRY_LATER ~RR#WSDC~ 28 3 ~0~                                           // prevent Wizard Slayers from dual-classing into mages
  SET_2DA_ENTRY_LATER ~RR#WSDC~ 28 4 ~0~                                           // prevent Wizard Slayers from dual-classing into thieves
  SET_2DA_ENTRY_LATER ~RR#WSDC~ 28 5 ~0~                                           // prevent Wizard Slayers from dual-classing into druids
  SET_2DA_ENTRIES_NOW ~RR#WSDC~ 3                                                  // Actually make the changes
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~CLABFI03.2DA~               ~override~                              // Wizard Slayer kit ability table
REPLACE_TEXTUALLY ~AP_SPCL131~ ~****~                                              // Remove old 1% Magic Resistance bonus
REPLACE_TEXTUALLY ~AP_SPCL819~ ~****~                                              // Remove old 5% Magic Resistance bonus
REPLACE_TEXTUALLY ~AP_SPCL133~ ~****~                                              // Remove old spell failure innate
SET_2DA_ENTRY_LATER ~RR#CLABFI03~ 3 1  ~AP_RR#WSSF1~                               // Grant new spell failure innate at level 1
SET_2DA_ENTRY_LATER ~RR#CLABFI03~ 4 1  ~GA_RR#WSSM1~                               // Grant Shatter Magic ability at level 1
SET_2DA_ENTRY_LATER ~RR#CLABFI03~ 4 8  ~GA_RR#WSSM1~                               // Grant Shatter Magic ability at level 8
SET_2DA_ENTRY_LATER ~RR#CLABFI03~ 4 16 ~GA_RR#WSSM1~                               // Grant Shatter Magic ability at level 16
SET_2DA_ENTRY_LATER ~RR#CLABFI03~ 4 24 ~GA_RR#WSSM1~                               // Grant Shatter Magic ability at level 24
SET_2DA_ENTRY_LATER ~RR#CLABFI03~ 4 32 ~GA_RR#WSSM1~                               // Grant Shatter Magic ability at level 32
SET_2DA_ENTRY_LATER ~RR#CLABFI03~ 4 40 ~GA_RR#WSSM1~                               // Grant Shatter Magic ability at level 40
 FOR (i = 1; i < 41; i += 1) BEGIN
   SET_2DA_ENTRY_LATER ~RR#CLABFI03~ 6 EVALUATE_BUFFER %i%  ~AP_RR#WSM2~           // Grant +2% Magic Resistance bonus at levels 1-40
 END
SET_2DA_ENTRIES_NOW ~RR#CLABFI03~ 1                                                // Actually make the changes
PRETTY_PRINT_2DA
BUT_ONLY_IF_IT_CHANGES





///////////////////////////////////////////////////////////////////////
// Wizard Slayer High Level Ability revision
///////////////////////////////////////////////////////////////////////


BEGIN @200 DESIGNATED 200 // Wizard Slayer High Level Ability revision

REQUIRE_PREDICATE ENGINE_IS ~tob bgee bg2ee iwdee~ @90                             // ToB engine required
REQUIRE_PREDICATE !GAME_IS ~iwdee~ @92                                             // Forbid installation on IWD:EE
FORBID_FILE ~override/_sw1h01.itm~ @91                                             // Forbid installation on Tutu



COPY    ~wsr/spl/rr#wsrm1.spl~               ~override~                            // Reflect Magic ability
  SAY NAME1 @210 SAY NAME2 @210 SAY UNIDENTIFIED_DESC @211 SAY DESC @211
  READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
  READ_LONG  0x6a "fx_off"
  FOR (index = 0 ; index < abil_num ; index = index + 1 ) BEGIN // cycle through abilities
    READ_SHORT  ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
    READ_SHORT  ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
    FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
     READ_SHORT ("%fx_off%" +        (0x30 * ("%index2%" + "%abil_fx_idx%"))) "opcode"
     READ_LONG  ("%fx_off%" + 0x04 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "param1"
      PATCH_IF (("%opcode%" = 206) AND ("%param1%" = 74011)) BEGIN                 // display string: "You cannot cast this spell..."
       SAY ("%fx_off%" + 0x04 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) @103      // You cannot use this ability until your innate magic resistance recovers.
      END
      PATCH_IF ((GAME_IS ~iwdee~) AND ("%opcode%" = 139) AND ("%param1%" = 14790)) BEGIN
       SAY ("%fx_off%" + 0x04 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) #16967  // display string: Magic Resistance Lowered
      END
    END
  END
COPY    ~wsr/bam/rr#wsrmb.bam~               ~override~                            // Reflect Magic BAM icon


COPY    ~wsr/spl/rr#wsab1.spl~               ~override~                            // Arcane Bane ability, Part 1
  SAY NAME1 @220 SAY NAME2 @220 SAY UNIDENTIFIED_DESC @221 SAY DESC @221
COPY    ~wsr/spl/rr#wsab2.spl~               ~override~                            // Arcane Bane ability, Part 2
COPY    ~wsr/spl/rr#wsab3.spl~               ~override~                            // Arcane Bane ability, Part 3
  READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
  READ_LONG  0x6a "fx_off"
  FOR (index = 0 ; index < abil_num ; index = index + 1 ) BEGIN // cycle through abilities
    READ_SHORT  ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
    READ_SHORT  ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
    FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
     READ_SHORT ("%fx_off%" +        (0x30 * ("%index2%" + "%abil_fx_idx%"))) "opcode"
     READ_LONG  ("%fx_off%" + 0x04 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "param1"
      PATCH_IF (("%opcode%" = 139) AND ("%param1%" = 8307)) BEGIN                  // display string: "Spell Failure"
       SAY ("%fx_off%" + 0x04 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) @223      // Loses a memorized spell
      END
    END
  END
COPY    ~wsr/eff/rr#wsab0.eff~               ~override~                            // Arcane Bane ability effect 1 (shell spell)
COPY    ~wsr/eff/rr#wsab1.eff~               ~override~                            // Arcane Bane ability effect 2 (shell spell)
COPY    ~wsr/eff/rr#wsdb1.eff~               ~override~                            // damage bonus vs. mages
COPY    ~wsr/eff/rr#wsdb2.eff~               ~override~                            // damage bonus vs. bards
COPY    ~wsr/eff/rr#wsdb3.eff~               ~override~                            // damage bonus vs. fighter/mages
COPY    ~wsr/eff/rr#wsdb4.eff~               ~override~                            // damage bonus vs. fighter/mages/thieves
COPY    ~wsr/eff/rr#wsdb5.eff~               ~override~                            // damage bonus vs. mage/thieves
COPY    ~wsr/eff/rr#wsdb6.eff~               ~override~                            // damage bonus vs. cleric/mages
COPY    ~wsr/eff/rr#wsdb7.eff~               ~override~                            // damage bonus vs. fighter/mage/clerics
COPY    ~wsr/eff/rr#wsdb8.eff~               ~override~                            // damage bonus vs. sorcerers
COPY    ~wsr/bam/rr#wsabb.bam~               ~override~                            // Arcane Bane BAM icon


COPY_EXISTING     ~LUABBR.2DA~               ~override~                            // master HLA table
 SET_2DA_ENTRY 30 1 1 ~Fi3~                                                        // assign Wizard Slayer a custom HLA table

ACTION_IF NOT FILE_EXISTS_IN_GAME ~tg#wizs.spl~ THEN BEGIN                         // In case Refinements isn't present
  COPY_EXISTING ~LUFI0.2DA~ ~override/LUFI3.2DA~                                   // clone the original fighter HLA table
END

COPY_EXISTING ~LUFI3.2DA~ ~override~                                               // Wizard Slayer HLA table
    REPLACE_TEXTUALLY ~SPCL904~ ~RR#WSRM1~                                         // Replace Resist Magic with Reflect Magic
    REPLACE_TEXTUALLY ~SPCL908~ ~RR#WSAB1~                                         // Replace War Cry with Arcane Bane
    PRETTY_PRINT_2DA
BUT_ONLY_IF_IT_CHANGES





///////////////////////////////////////////////////////////////////////
// Revised Wizard Slayer item restrictions
///////////////////////////////////////////////////////////////////////


BEGIN @301 DESIGNATED 301 // Minimal changes
SUBCOMPONENT @300  // Revised Wizard Slayer item restrictions
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob bgee bg2ee iwdee~ @92                         // SoA or ToB engine


ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
              ~AMUL19~                                                             // Amulet of 5% Magic Resistance
              ~AMUL20~                                                             // Kaligun's Amulet of Magic Resistance
              ~AMUL27~                                                             // Amulet of Seldarine
              ~CLCK05~                                                             // Cloak of Balduran
             ~_CLCK05~                                                             // Cloak of Balduran (Tutu version)
              ~POTN34~                                                             // Potion of Magic Protection
             ~_POTN34~                                                             // Potion of Magic Protection (Tutu version)
             ~USPOT3A~                                                             // Potion of Magical Resistance (IWD:EE)
             ~USPOT4A~                                                             // Potion of Arcane Absorption (IWD:EE)
             ~USPOT4C~                                                             // Potion of Aura Enhancement (IWD:EE)
             ~USPOT5A~                                                             // Potion of Dissipation (IWD:EE)
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%.itm~ BEGIN                                   // if the designated file with an ITM extension exists
 COPY_EXISTING ~%file%.itm~ ~override~
   PATCH_IF (%SOURCE_SIZE% > 0x71) THEN BEGIN                                      // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
     READ_BYTE  0x2f "use"
     WRITE_BYTE 0x2f ("%use%" BAND  0b11111101)                                    // allows Wizard Slayers to use this item
     READ_STRREF IDENTIFIED_DESC temp_desc                                         // description update
      INNER_PATCH_SAVE new_desc "%temp_desc%" BEGIN
        GET_STRREF 25152 ~ws_desc~                                                 // Wizard Slayer kit strref
        REPLACE_TEXTUALLY CASE_INSENSITIVE ~%ws_desc%~ ~ ~                         // remove Wizard Slayer from the "Not Usabable By:" section
      END
  SAY_EVALUATED IDENTIFIED_DESC "%new_desc%"
   END                                                                             // ends file size check
 BUT_ONLY_IF_IT_CHANGES
END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block



// ************************************************************** Subcomponent marker ************************************************************



BEGIN @302 DESIGNATED 302 // Moderate changes
SUBCOMPONENT @300  // Revised Wizard Slayer item restrictions
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob bgee bg2ee iwdee~ @92                         // SoA or ToB engine


ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
              ~AMUL19~                                                             // Amulet of 5% Magic Resistance
              ~AMUL20~                                                             // Kaligun's Amulet of Magic Resistance
              ~AMUL27~                                                             // Amulet of Seldarine
              ~AMUL25~                                                             // Amulet of Spell Warding
              ~AMUL27~                                                             // Amulet of Seldarine
              ~BELT10~                                                             // Belt of Inertial Barrier
              ~CLCK05~                                                             // Cloak of Balduran
             ~_CLCK05~                                                             // Cloak of Balduran (Tutu version)
              ~POTN33~                                                             // Potion of Magic Blocking
             ~_POTN33~                                                             // Potion of Magic Blocking (Tutu version)
              ~POTN34~                                                             // Potion of Magic Protection
             ~_POTN34~                                                             // Potion of Magic Protection (Tutu version)
              ~POTN35~                                                             // Potion of Magic Shielding
             ~_POTN35~                                                             // Potion of Magic Shielding (Tutu version)
              ~RING34~                                                             // Ring of Spell Turning
             ~USPOT3A~                                                             // Potion of Magical Resistance (IWD:EE)
             ~USPOT4A~                                                             // Potion of Arcane Absorption (IWD:EE)
             ~USPOT4C~                                                             // Potion of Aura Enhancement (IWD:EE)
             ~USPOT5A~                                                             // Potion of Dissipation (IWD:EE)
             ~POTN15~                                                              // Red Potion (IWD:EE)
             ~URING4B~                                                             // Ring of the Protector +2 (IWD:EE)
             ~URING1B~                                                             // Ring of Lesser Resistance (IWD:EE)
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%.itm~ BEGIN                                   // if the designated file with an ITM extension exists
 COPY_EXISTING ~%file%.itm~ ~override~
   PATCH_IF (%SOURCE_SIZE% > 0x71) THEN BEGIN                                      // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
     READ_BYTE  0x2f "use"
     WRITE_BYTE 0x2f ("%use%" BAND  0b11111101)                                    // allows Wizard Slayers to use this item
     READ_STRREF IDENTIFIED_DESC temp_desc                                         // description update
      INNER_PATCH_SAVE new_desc "%temp_desc%" BEGIN
        GET_STRREF 25152 ~ws_desc~                                                 // Wizard Slayer kit strref
        REPLACE_TEXTUALLY CASE_INSENSITIVE ~%ws_desc%~ ~ ~                         // remove Wizard Slayer from the "Not Usabable By:" section
      END
  SAY_EVALUATED IDENTIFIED_DESC "%new_desc%"
   END                                                                             // ends file size check
 BUT_ONLY_IF_IT_CHANGES
END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block




////////////////////////////////////
// BG2-style icons for WSR content
////////////////////////////////////

BEGIN @999 DESIGNATED 999
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob bgee bg2ee iwdee~ @92                               // SoA or ToB engine

COPY    ~wsr/bam/bg2s~   ~override~




// *********************************** END OF FILE ***********************************************
